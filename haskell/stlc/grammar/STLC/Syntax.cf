comment "--" ;
comment "{-" "-}" ;

AProgram. Program ::= [Command] ;

CommandCheck. Command ::= "check" Expr ;
CommandCompute. Command ::= "compute" Expr ;
terminator Command ";" ;

token NatIdent digit+ ;
token TrueIdent {"true"} ;
token FalseIdent {"false"} ;
token VarIdent letter (letter | digit | '_' | '\'')* ;


EVar. Expr3 ::= VarIdent ;
EConstNat. Expr3 ::= NatIdent ;
EConstTrue. Expr3 ::= TrueIdent ;
EConstFalse. Expr3 ::= FalseIdent ;

EAdd. Expr2 ::= Expr2 "+" Expr2 ;
ESub. Expr2 ::= Expr2 "-" Expr2 ;
EIsZero. Expr2 ::= "iszero" "(" Expr ")" ;

ELam. Expr1 ::= "Î»" Pattern "." ScopedExpr ;
EApp. Expr ::= Expr1 Expr ;
EIf. Expr ::= "if" Expr "then" Expr "else" Expr ;
ELet. Expr ::= "let" Pattern "=" Expr "in" ScopedExpr ;

ETyped. Expr ::= Expr ":" Type ;

coercions Expr 3 ;


AScopedExpr. ScopedExpr ::= Expr ;


PatternVar. Pattern ::= VarIdent ;

TFunc. Type ::= Type1 "->" Type ;
TNat. Type1 ::= "Nat" ;
TBool. Type1 ::= "Bool" ;

coercions Type 1 ;
