-- comment "--" ;
-- comment "{-" "-}" ;

Program. Program ::= [Command] ;

CommandCheck. Command ::= "check" Expr ;
CommandCompute. Command ::= "compute" Expr ;
terminator Command ";" ;

token VarIdent letter (letter | digit | '_')* ;
token UVarIdent '?' letter (letter | digit | '_')* ;


EVar. Expr3 ::= VarIdent ;
EConstNat. Expr3 ::= Integer ;
EConstTrue. Expr3 ::= "true" ;
EConstFalse. Expr3 ::= "false" ;

EAdd. Expr2 ::= Expr2 "+" Expr3 ;
ESub. Expr2 ::= Expr2 "-" Expr3 ;
EIsZero. Expr2 ::= "iszero" "(" Expr ")" ;

ELam. Expr1 ::= "\\" Pattern "." ScopedExpr ;
EApp. Expr1 ::= Expr1 Expr2 ;
EIf. Expr1 ::= "if" Expr1 "then" Expr1 "else" Expr1 ;
ELet. Expr1 ::= "let" Pattern "=" Expr1 "in" ScopedExpr ;
EFor. Expr1 ::= "for" Pattern "in" "[" Expr1 ".." Expr1 "]" "do" ScopedExpr ;

ETyped. Expr ::= Expr1 ":" Type ;

coercions Expr 3 ;


-- Types

TForAll. Type ::= "forall" TypePattern "." ScopedType ;
TFunc. Type1 ::= Type2 "->" Type1 ;
TNat. Type2 ::= "Nat" ;
TBool. Type2 ::= "Bool" ;
TVar. Type2 ::= VarIdent ;
TUVar. Type2 ::= UVarIdent ; 

coercions Type 2 ;


-- Scoped Terms, Patterns

ScopedExpr. ScopedExpr ::= Expr ;
ScopedType. ScopedType ::= Type1 ;


PatternVar. Pattern ::= VarIdent ;
PatternType. TypePattern ::= VarIdent ;

